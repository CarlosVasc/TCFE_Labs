*-----------------------------------------------------------------------------
*
* To use a subcircuit, the name must begin with 'X'.  For example:
* X1 1 2 3 4 5 uA741
*
* connections:   non-inverting input
*                |  inverting input
*                |  |  positive power supply
*                |  |  |  negative power supply
*                |  |  |  |  output
*                |  |  |  |  |
.subckt uA741    1  2  3  4  5
*
  c1   11 12 8.661E-12
  c2    6  7 30.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61E6 -10E6 10E6 10E6 -10E6
  ga    6  0 11 12 188.5E-6
  gcm   0  6 10 99 5.961E-9
  iee  10  4 dc 15.16E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 5.305E3
  rc2   3 12 5.305E3
  re1  13 10 1.836E3
  re2  14 10 1.836E3
  ree  10 99 13.19E6
  ro1   8  5 50
  ro2   7 99 100
  rp    3  4 18.16E3
  vb    9  0 dc 0
  vc    3 53 dc 1
  ve   54  4 dc 1
  vlim  7  8 dc 0
  vlp  91  0 dc 40
  vln   0 92 dc 40
.model dx D(Is=800.0E-18 Rs=1)
.model qx NPN(Is=800.0E-18 Bf=93.75)
.ends



.options savecurrents

Vcc vcc 0 5.0
Vee vee 0 -5.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)

X1 plus minus vcc vee out uA741

C1 plus in 220n
C2 out2 0 110n
R1 plus 0 1k
R2 out out2 1k
R3 minus 0 1k
R4 out minus 100k

.op
.end

.control

set filetype=ascii
run


******************************************

*makes plots in color
set hcopypscolor=0
set color0=white
set color1=black
set color2=red
set color3=blue
set color4=violet
set color5=rgb:3/8/0
set color6=rgb:4/0/0

******************************************


* operation point
op
print all



* time analysis
tran 1e-5 1e-2

*plot v(out)
*hardcopy vin_time.ps v(in)



* frequency analysis
ac dec 10 1 100MEG

*plot vdb(out)
*plot vp(out)
*hardcopy vin.ps v(in)
*hardcopy vout2.ps v(out2)

hardcopy vdbo2f.ps vdb(out2)
echo "vdbo2f_FIG"
hardcopy vpho2f.ps ph(out2)*180/pi
echo "vpho2f_FIG"




* gain deviation
meas ac gainat1000 FIND v(out2) AT=1000
let gaindeviation='abs(gainat1000-40)'
print gaindeviation


* central frequency
meas ac ymax MAX vdb(out2) from=0 to=100MEG
let y='ymax-3'
meas ac f1 WHEN vdb(out2)=y
meas ac f2 WHEN vdb(out2)=y Fall=LAST
let centralfreq='sqrt((f1*f2))'
print centralfreq
let centralfreqdev='abs(centralfreq-1000)'
print centralfreqdev


* real gain (at central frequency)
meas ac gain Find vdb(out2) AT=centralfreq


*input impedance in ohm at near central frequency
echo "op_Zin_TAB"
let Zr_in= Re(-v(in)[40]/i(Vin)[40])
let Zim_in= Im(-v(in)[40]/i(Vin)[40])
echo "Zin = $&Zr_in + $&Zim_in j"
echo "op_Zin_END"

* TAB of results
echo  "op_results_TAB"
echo "Gain at 1kHz = $&gainat1000"
echo "Gain deviation=$&gaindeviation"
echo "Central Frequency= $&centralfreq"
echo "Central frequency deviation=$&centralfreqdev"
echo  "op_results_END"



.endc 

